apply plugin: 'java'
apply plugin: 'com.google.protobuf'

// define repositories
repositories {
    mavenCentral()
}

// define dependencies
dependencies {
    compile 'com.google.protobuf:protobuf-java:3.4.0'
    compile 'io.grpc:grpc-protobuf:1.6.1' // for com.google.common.primitives
}

// build script
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

// protobuf protoc
protobuf{
    protoc {
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
}


// define environment variables
def jniClass = "com.bushpath.anamnesis.checksum.NativeChecksumCRC32"
def nativeIncludeDir = "build/generated"
def javaHome = "/usr/lib/jvm/default-java"
      
// generate jni headers        
task generateJniHeaders(type: Exec) {
    def classpath = "build/classes/main/"
    commandLine "javah", "-d", "$nativeIncludeDir", "-classpath", classpath, 
        "$jniClass"       

    dependsOn classes
}

// compile cpp program
task compileCpp(type: Exec) {
    commandLine "g++", "-o", "$nativeIncludeDir/libcrc.so",
        "-lc", "-shared", "-fPIC",      
        "-I", "$nativeIncludeDir", "-I", "$javaHome/include", 
        "-I", "$javaHome/include/linux/", "src/main/cpp/crc.cpp"

    dependsOn generateJniHeaders    
}

// define jar depends
jar.dependsOn generateJniHeaders, compileCpp
